buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id('java')
    id('eclipse')
    id('maven-publish')
    id('net.minecraftforge.gradle') version '5.1.+'
}

apply plugin:'org.spongepowered.mixin'

minecraft {
    mappings channel: 'official', version: '1.19.2'
    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
			mods {
                amethysttoolsmod {
                    source sourceSets.main
                    source project(':Xplat').sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                amethysttoolsmod {
                    source sourceSets.main
                    source project(':Xplat').sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', 'amethysttoolsmod', '--all', '--output', file('src/generated/resources/')
            environment 'target', 'fmluserdevdata'
            mods {
                amethysttoolsmod {
                    source sourceSets.main
                    source project(':Xplat').sourceSets.main
                }
            }
        }
    }
}

mixin {
    add sourceSets.main, "mixins.amethysttoolsmod.refmap.json"
    config "amethysttoolsmod.forge.mixins.json"
    config "amethysttoolsmod.xplat.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }


final def xplat = project(":Xplat")
final def xplatMain = xplat.sourceSets.main

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.2.0'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    compileOnly(xplat)
}

compileJava {
    source xplatMain.allSource
}

processResources {
    from xplatMain.resources
}

jar {
    manifest {
        attributes([
                "MixinConfigs"            : "amethysttoolsmod.forge.mixins.json,amethysttoolsmod.xplat.mixins.json"
        ])
    }
    finalizedBy('reobfJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}
